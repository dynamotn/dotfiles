# vim:foldmethod=marker:foldmarker={,}
## Some environment variables {
# Path of Fish config
set -gx CONFIG_PATH (dirname (readlink -f (status --current-filename)))

{{- if eq .chezmoi.os "linux" }}
# XDG base directories
set -gx XDG_CACHE_HOME $HOME/.cache
set -gx XDG_CONFIG_HOME $HOME/.config
set -gx XDG_DATA_HOME $HOME/.local/share
set -gx XDG_STATE_HOME $HOME/.local/state
{{- if eq .chezmoi.osRelease.id "gentoo" }}
set -gx XDG_RUNTIME_DIR /tmp/user-(id -u)
{{- else }}
set -gx XDG_RUNTIME_DIR /run/user/(id -u)
{{- end }}
{{- end }}

# GPG interface to insert passphrase
set -gx GPG_TTY (tty)

# GO workspace
set -gx GOPATH $HOME/.go

# PATH
function __add_folder_to_manpath --description "Add folder to MANPATH"
    if test (count $argv) -ne 1
        echo 'Must has only one argument'
        return 1
    end
    if test -e $argv[1]
        set -gx MANPATH $argv[1] $MANPATH
    end
end

fish_add_path -pPg /opt/homebrew/bin
fish_add_path -pPg /opt/homebrew/sbin
fish_add_path -pPg $HOME/.local/bin
fish_add_path -pPg $CONFIG_PATH/../scripts/bin
fish_add_path -pPg $HOME/.fzf/bin
fish_add_path -pPg $HOME/.yarn/bin
fish_add_path -pPg $HOME/.cargo/bin
fish_add_path -pPg $HOME/.go/bin
fish_add_path -pPg $HOME/.local/share/nvim/mason/bin
fish_add_path -pPg $HOME/.krew/bin

__add_folder_to_manpath $HOME/.local/man
__add_folder_to_manpath $CONFIG_PATH/../scripts/man

# Package manager
type -q brew; and brew shellenv | source
if command -sq mise
    set -gx MISE_PREFER_OFFLINE true
    mise activate fish | source
    command -sq usage; and mise completion fish | source
end

# Set default editor
abbr -a v vim
command -sq nvim; and begin
    set -gx EDITOR nvim
    alias vim nvim
    alias vimdiff 'nvim -d'
    set -gx MANPAGER 'nvim +Man!'
end
or begin
    set -gx EDITOR vim
    set -gx MANPAGER 'vim -M +MANPAGER -'
end
## }

## Fish or shell miscellaneous config {
# Enable vi key bindings
if test -n "$TERM"
    fish_vi_key_bindings
end

# grc for color output
if type -q grc
    set -gx grcplugin_ls --color -Chl --group-directories-first
    set -x grcplugin_df -H
end

# Color for Virtual console
if test "$TERM" = linux; and test $CONFIG_PATH/console_color
    for i in (sed -n "s/.*\*color\([0-9]\{1,\}\).*#\([0-9a-fA-F]\{6\}\).*/\1 \2/p" $CONFIG_PATH/console_color | awk '$1 < 16 {printf "\\\e]P%X%s", $1, $2}')
        echo -en "$i"
    end
    set -g theme_nerd_fonts no
end
## }

## Other tools {
if status is-interactive
    # Theme
    fish_config theme choose catppuccin
    {{- if not .darkMode }}
    set -gx canaudua_host_context_fg brwhite
    set -gx canaudua_pwd_bg white
    set -gx canaudua_pwd_fg black
    set -gx canaudua_git_fg brwhite
    {{- end }}
    # Glamour integrated tools
    set -gx GLAMOUR_STYLE "$HOME/.config/glamour/catppuccin.json"
    # Plugins
    set sponge_successful_exit_codes 0 127
    set sponge_purge_only_on_exit true
    # Vale
    set -gx VALE_CONFIG_PATH "$HOME/.config/vale.ini"
    set -gx VALE_STYLES_PATH "$HOME/.local/share/vale"

    # Hooks
    # Fzf config
    type -q __fzf_setup; and __fzf_setup
    {{- $theme := .catppuccin.macchiato }}
    {{- if not .darkMode }}
    {{-   $theme = .catppuccin.latte }}
    {{- end }}
    set -Ux FZF_COLOR_OPTS "\
        --color=bg+:{{ $theme.surface0 }},bg:{{ $theme.base }},spinner:{{ $theme.rosewater }},hl:{{ $theme.red}} \
        --color=fg:{{ $theme.text }},header:{{ $theme.red }},info:{{ $theme.mauve }},pointer:{{ $theme.rosewater }} \
        --color=marker:{{ $theme.lavender }},fg+:{{ $theme.text }},prompt:{{ $theme.mauve }},hl+:{{ $theme.red }} \
        --color=selected-bg:{{ $theme.surface1 }} \
        --color=border:{{ $theme.overlay0 }},label:{{ $theme.text }}"
    set -gx FZF_DEFAULT_OPTS "\
        $FZF_COLOR_OPTS \
        --cycle --layout=reverse --preview-window=wrap \
        --marker=\"*\" --prompt=\"î‚° \" --pointer=\"=>\""
    type -q tmux; and test -n "$TMUX"; and set -g FZF_TMUX 1
    if type -q fdfind
        alias fd fdfind
    end
    if type -q fd
        set -gx FZF_FIND_FILE_COMMAND "fd -Ht f -E .git . \$dir 2> /dev/null"
        set -gx FZF_CD_COMMAND "fd -t d . \$dir 2> /dev/null"
        set -gx FZF_CD_WITH_HIDDEN_COMMAND "fd -Ht d -E .git . \$dir 2> /dev/null"
        fd --gen-completions fish | source
    end

    # Zoxide
    type -q zoxide; and zoxide init fish | source
    # Navi
    type -q navi; and navi widget fish | source
    # Projekt
    if type -q projekt
        projekt init fish | source
        projekt completion fish | source
        t completion fish | source
        b completion fish | source
    end
    # ov
    if type -q ov
        set -gx PAGER ov
        ov --completion fish | source
    end
    # Vivid
    type -q vivid; and set -gx LS_COLORS (vivid generate catppuccin-{{ if .darkMode }}macchiato{{ else }}latte{{ end }})
    # Zellij
    if type -q zellij
        set -gx ZELLIJ_AUTO_ATTACH true
        zellij setup --generate-completion fish | source
    end

    # Other completions generation
    # kubectl
    type -q kubectl; and kubectl completion fish | source
    # task
    type -q task; and task --completion fish | source
    # bat
    type -q bat; and bat --completion fish | source
    # ltcc
    type -q ltcc; and ltcc completion --shell fish | source
    # rg
    type -q rg; and rg --generate=complete-fish | source
    # chezmoi
    type -q chezmoi; and chezmoi completion fish | source
    # rbw
    type -q rbw; and rbw gen-completions fish | source
end
## }

## Load per-machine & secret config {
if test -e $HOME/.config/fish/per_machine.fish
    source $HOME/.config/fish/per_machine.fish
end
## }
